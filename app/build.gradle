plugins {
    id 'com.gladed.androidgitversion' version '0.4.14'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

androidGitVersion {
    codeFormat 'MNNPPPBBB'
    format "%tag%%.count%%-dirty%"
    if (System.getenv('CI') == null) {
        format "%tag%%.count%%-dirty%"
    } else {
        //if it's a CI build ignore dirty because bundle execute demands bundler/gems to be up to date (forces it server-side) --> gemlock always changed
        format "%tag%%.count%"
    }
    //when their are uncommitted changes add dirty to version name
    untrackedIsDirty = true
}

/**
 * Check if a signing.gradle file exists and import it
 */
if (file(rootProject.projectDir.getAbsolutePath() + '/signing.gradle').exists()) {
    apply from: rootProject.projectDir.getAbsolutePath() + '/signing.gradle'
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "be.appwise.simplifiedPokedex"
        minSdkVersion 21
        targetSdkVersion 29
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        flavorDimensions "versionCode"

        // generate versionCode and versionName from defined major, minor, versionPatch, versionBuild
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
        vectorDrawables.setUseSupportLibrary(true)
    }

    productFlavors {
        dev {
            buildConfigField "String", "API_HOST", "\"https://pokeapi.co/api/v2/\""

            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            dimension 'env'
            signingConfig signingConfigs.release
        }
        prod {
            buildConfigField "String", "API_HOST", "\"https://pokeapi.co/api/v2/\""
            dimension 'env'
            signingConfig signingConfigs.release
        }
        stg {
            buildConfigField "String", "API_HOST", "\"https://pokeapi.co/api/v2/\""

            applicationIdSuffix ".stg"
            versionNameSuffix "-stg"
            dimension 'env'
            signingConfig signingConfigs.release
        }
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    flavorDimensions 'env'
    /**
     * Fix build filenames
     * add flavor, version name and code to final apk file
     */
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def file = output.outputFile
            if (file != null && file.name.endsWith('.apk')) {
                def suffix = "_" + defaultConfig.applicationId + "_" + defaultConfig.versionName + "_" + defaultConfig.versionCode + ".apk"
                def outputFileNameTest = file.name.replace(".apk", suffix)
                outputFileName = new File(outputFileNameTest)
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':core')

    //Glide
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    //image compressing
    implementation 'androidx.exifinterface:exifinterface:1.0.0'

    implementation 'androidx.preference:preference:1.0.0'

    // Recyclerview animations
    implementation 'jp.wasabeef:recyclerview-animators:3.0.0'

    kapt "androidx.room:room-compiler:2.2.6"

    debugImplementation "com.github.donfuxx:Mockinizer:1.6.0"
    implementation "com.squareup.okhttp3:mockwebserver:4.9.1"

    implementation "androidx.test:core:1.2.0"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.2.1"

    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.2.1"

    implementation platform('com.google.firebase:firebase-bom:27.0.0')

    implementation 'com.google.firebase:firebase-analytics-ktx'
}
